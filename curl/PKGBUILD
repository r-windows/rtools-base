# Maintainer: Jeroen Ooms

pkgbase=curl
pkgname=('curl' 'libcurl' 'libcurl-devel' 'curl-ca-bundle')
pkgver=7.64.1
pkgrel=9800
pkgdesc="Multi-protocol file transfer utility"
arch=('i686' 'x86_64')
url="https://curl.haxx.se"
license=('MIT')
makedepends=('openssl-devel' 'zlib-devel' 'autotools')
options=('!libtool' 'strip' '!debug')
source=("https://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2"
        curl-7.55.1-msys2.patch
        curl-7.58.0-libpsl-static-libs.patch
        curl-7.60.0-gssapi-static-libs.patch
        https://curl.haxx.se/ca/cacert.pem)
sha256sums=('4cc7c738b35250d0680f29e93e0820c4cb40035f43514ea3ec8d60322d41a45d'
            '2e6e2245a10890f8a8b1ec84dbe86dd2415b05359a93e69be9c7b90768e6ccc0'
            'ad3d76013c2dd683c44ad4cdc5108ea5218056c87b66f6ed2a90502e785a39af'
            'd58a94556c031e550403ed13691305983bf83493f15fb8c35615e59bf265bbf7'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/curl-7.58.0-libpsl-static-libs.patch
  patch -p1 -i ${srcdir}/curl-7.55.1-msys2.patch
  patch -p1 -i ${srcdir}/curl-7.60.0-gssapi-static-libs.patch
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --enable-static \
    --disable-debug \
    --enable-optimize \
    --enable-ipv6 \
    --disable-hidden-symbols \
    --disable-ares \
    --without-libidn2 \
    --without-libmetalink \
    --without-librtmp \
    --without-libssh2 \
    --without-spnego \
    --without-random \
    --with-ssl \
    --without-winidn \
    --without-gnutls \
    --without-nghttp2 \
    --with-zlib \
    --without-libpsl \
    --with-ca-bundle=/usr/ssl/certs/curl-ca-bundle.crt \
    ac_cv_func_setmode=yes

  make
  make DESTDIR="${srcdir}/dest" install
}

package_curl() {
  depends=('libcurl' 'openssl' 'zlib')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/

  # license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libcurl() {
  pkgdesc="Multi-protocol file transfer library (runtime)"
  depends=('openssl' 'zlib' 'curl-ca-bundle')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libcurl-devel() {
  pkgdesc="Libcurl headers and libraries"
  depends=("libcurl=${pkgver}" 'openssl-devel' 'zlib-devel')
  options=('staticlibs')
  groups=('development')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

package_curl-ca-bundle() {
  pkgdesc="Lightweight alternative to ca-certificates"

  mkdir -p ${pkgdir}/usr/ssl/certs
  install -m644 ${srcdir}/cacert.pem ${pkgdir}/usr/ssl/certs/curl-ca-bundle.crt
}
